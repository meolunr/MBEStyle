apply plugin: 'com.android.application'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "me.iacn.mbestyle"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 26
        versionName "1.7.3"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    def file = project.rootProject.file('config.prop')
    def leanCloudId = '"+1S"'
    def leanCloudKey = '"+1S"'

    if (file.exists()) {
        Properties prop = new Properties()
        InputStream stream = file.newDataInputStream()
        prop.load(stream)
        stream.close()

        leanCloudId = prop.getProperty('leanCloudId')
        leanCloudKey = prop.getProperty('leanCloudKey')
    }

    buildTypes.each {
        it.buildConfigField 'String', 'LEANCLOUD_ID', leanCloudId
        it.buildConfigField 'String', 'LEANCLOUD_KEY', leanCloudKey
    }

    // 修改生成 Apk 的文件名
    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "MBEStyle_${defaultConfig.versionName}.apk"
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:support-v13:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'

    implementation 'io.reactivex.rxjava2:rxjava:2.1.3'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'

    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.2.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.2.0'

    implementation 'com.github.bumptech.glide:glide:4.0.0'
    implementation 'com.wang.avi:library:2.1.3'
    implementation 'com.google.code.gson:gson:2.8.1'
    implementation 'moe.feng:AlipayZeroSdk:1.1'
}